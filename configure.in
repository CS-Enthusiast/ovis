dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.57)
AC_INIT(ldms-utils, 1.0.0, tom@ogc.us)
AC_CONFIG_SRCDIR([src/ldms.h])
AC_CONFIG_AUX_DIR(config)
AM_CONFIG_HEADER(ldms_config.h)
AM_INIT_AUTOMAKE(ldms-utils, 1.0.0)
AM_PROG_LIBTOOL

AC_ARG_ENABLE(rdma, [  --disable-rdma     Disable the RDMA transport],
        [       if test x$enableval = xno ; then
                        disable_rdma=yes
			echo RDMA transport is disabled
                fi
        ])

AC_ARG_ENABLE(sock, [  --disable-sock     Disable the sockets transport],
        [       if test x$enableval = xno ; then
                        disable_sock=yes
			echo Socket transport is disabled
                fi
        ])

AC_ARG_ENABLE(mmap, [  --disable-mmap     Disable peristent local data sets (i.e. mmap).],
        [       if test x$enableval = xno ; then
                        disable_mmap=yes
			echo The mmap API is not supported on this system.
                fi
        ])

dnl Checks for programs
AC_PROG_CC
AC_CHECK_SIZEOF(long)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST

dnl Check for pthread mutex/spinlock support
AC_CHECK_LIB(pthread,pthread_spin_init, [], [])
AM_CONDITIONAL([HAVE_SPINLOCK_T],[test "$ac_cv_lib_pthread_pthread_spin_init" != "no"])

dnl Checks for libraries
if test "$disable_rdma" != "yes"
then
AC_CHECK_LIB(ibverbs, ibv_get_device_list, [],
    AC_MSG_ERROR([ibv_get_device_list() not found.  ldms-utils requires libibverbs.]))

dnl Checks for header files.
AC_CHECK_HEADER(infiniband/driver.h, [],
    AC_MSG_ERROR([<infiniband/driver.h> not found.  Is libibverbs installed?]))
AC_HEADER_STDC

dnl Checks for library functions
AC_CHECK_FUNCS(ibv_read_sysfs_file)

dnl Now check if for libibverbs device library extension
dummy=if$$
cat <<IBV_VERSION > $dummy.c
#include <infiniband/driver.h>
IBV_DEVICE_LIBRARY_EXTENSION
IBV_VERSION
IBV_DEVICE_LIBRARY_EXTENSION=`$CC $CPPFLAGS -E $dummy.c 2> /dev/null | tail -1`
rm -f $dummy.c
if test $IBV_DEVICE_LIBRARY_EXTENSION = IBV_DEVICE_LIBRARY_EXTENSION; then
    AC_MSG_ERROR([IBV_DEVICE_LIBRARY_EXTENSION not defined.  Is libibverbs new enough?])
fi
AC_SUBST(IBV_DEVICE_LIBRARY_EXTENSION)
fi

AC_CACHE_CHECK(whether ld accepts --version-script, ac_cv_version_script,
    if test -n "`$LD --help < /dev/null 2>/dev/null | grep version-script`"; then
        ac_cv_version_script=yes
    else
        ac_cv_version_script=no
    fi)

AM_CONDITIONAL([ENABLE_RDMA], [test "$disable_rdma" != "yes"])
AM_CONDITIONAL([ENABLE_SOCK], [test "$disable_sock" != "yes"])
AM_CONDITIONAL([ENABLE_MMAP], [test "$disable_mmap" != "yes"])
AM_CONDITIONAL(HAVE_LD_VERSION_SCRIPT, test "$ac_cv_version_script" = "yes")
AC_CONFIG_FILES([Makefile ldms-utils.spec])
AC_OUTPUT

